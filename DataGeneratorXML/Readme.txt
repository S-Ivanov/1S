Программа для генерации тестовых данных и сохранения их в виде XML-файла.

Примеры загрузки XML-файла в 1С:

1) чтение всего содержимого XML-файла в память, прямой доступ к тэгам и атрибутам

ЧтениеXML = Новый ЧтениеXML;
ЧтениеXML.ОткрытьФайл("Данные.xml");
ПостроительDOM = Новый ПостроительDOM;
ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
ЧтениеXML.Закрыть();
Для Каждого xmlКомпания Из ДокументDOM.ЭлементДокумента.ДочерниеУзлы Цикл
    Если НЕ (xmlКомпания.ТипУзла = ТипУзлаDOM.Элемент И xmlКомпания.ИмяУзла = "Компания") Тогда
        Продолжить;
    КонецЕсли;
    НоваяКомпания = Справочники.Компании.СоздатьЭлемент();
    НоваяКомпания.Код = 
        xmlКомпания.Атрибуты.ПолучитьИменованныйЭлемент("код").ЗначениеУзла;
    НоваяКомпания.Наименование = 
        xmlКомпания.Атрибуты.ПолучитьИменованныйЭлемент("наименование").ЗначениеУзла;
    НоваяКомпания.Записать();

    Для Каждого xmlПродукция из xmlКомпания.ДочерниеУзлы Цикл
        Если НЕ (xmlПродукция.ТипУзла = ТипУзлаDOM.Элемент 
            И xmlПродукция.ИмяУзла = "Продукция") Тогда
                Продолжить;
        КонецЕсли;
        НоваяПродукция = Справочники.Продукция.СоздатьЭлемент();
        НоваяПродукция.Код = 
            xmlПродукция.Атрибуты.ПолучитьИменованныйЭлемент("код").ЗначениеУзла;
        НоваяПродукция.Наименование = 
            xmlПродукция.Атрибуты.ПолучитьИменованныйЭлемент("наименование").ЗначениеУзла;
        НоваяПродукция.Компания = НоваяКомпания.Ссылка;
        НоваяПродукция.Записать();
    КонецЦикла;
КонецЦикла;

2) последовательное чтение содержимого XML-файла, прямой доступ к тэгам и атрибутам

ЧтениеXML = Новый ЧтениеXML;
ЧтениеXML.ОткрытьФайл("Данные.xml");
НоваяКомпания = Неопределено;
Пока ЧтениеXML.Прочитать() Цикл
    Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
        Если ЧтениеXML.Имя = "Компания" Тогда
            НоваяКомпания = Справочники.Компании.СоздатьЭлемент();
            Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
                Если ЧтениеXML.Имя = "код" Тогда
                    НоваяКомпания.Код = ЧтениеXML.Значение;
                ИначеЕсли ЧтениеXML.Имя = "наименование" Тогда
                    НоваяКомпания.Наименование = ЧтениеXML.Значение;
                КонецЕсли;
            КонецЦикла;
            НоваяКомпания.Записать();
        ИначеЕсли ЧтениеXML.Имя = "Продукция" И НоваяКомпания <> Неопределено Тогда
            НоваяПродукция = Справочники.Продукция.СоздатьЭлемент();
            Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
                Если ЧтениеXML.Имя = "код" Тогда
                    НоваяПродукция.Код = ЧтениеXML.Значение;
                ИначеЕсли ЧтениеXML.Имя = "наименование" Тогда
                    НоваяПродукция.Наименование = ЧтениеXML.Значение;
                КонецЕсли;
            КонецЦикла;
            НоваяПродукция.Компания = НоваяКомпания.Ссылка;
            НоваяПродукция.Записать();
        КонецЕсли;
    КонецЕсли;
КонецЦикла;
ЧтениеXML.Закрыть();

3) чтение всего содержимого XML-файла в память, десериализация 1С-объектов с помощью XDTO

ЧтениеXML = Новый ЧтениеXML;
ЧтениеXML.ОткрытьФайл("C:\Проекты\1S\DataGeneratorXML\bin\Debug\Data.xml");
ТипКомпанииПродукция = ФабрикаXDTO.Тип("http://localhost/xdto", "Данные");
КомпанииПродукцияXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ТипКомпанииПродукция);

ПоследовательностьКомпании = КомпанииПродукцияXDTO.Последовательность();
КоличествоКомпании = ПоследовательностьКомпании.Количество();
Для I = 0 По КоличествоКомпании - 1 Цикл
    КомпанияXDTO = ПоследовательностьКомпании.ПолучитьЗначение(I);
    НоваяКомпания = Справочники.Компании.СоздатьЭлемент();
    НоваяКомпания.Код = КомпанияXDTO.код;
    НоваяКомпания.Наименование = КомпанияXDTO.наименование;
    НоваяКомпания.Записать();
    НоваяКомпанияСсылка = НоваяКомпания.Ссылка;
	
    ПоследовательностьПродукция = КомпанияXDTO.Последовательность();
    КоличествоПродукция = ПоследовательностьПродукция.Количество();
    Для J = 0 По КоличествоПродукция - 1 Цикл
        ПродукцияXDTO = ПоследовательностьПродукция.ПолучитьЗначение(J);
        НоваяПродукция = Справочники.Продукция.СоздатьЭлемент();
        НоваяПродукция.Код = ПродукцияXDTO.код;
        НоваяПродукция.Наименование = ПродукцияXDTO.наименование;
        НоваяПродукция.Компания = НоваяКомпанияСсылка;
        НоваяПродукция.Записать();
    КонецЦикла;
КонецЦикла;
